DEMO   ITALIAN   CONFIG (1.0)
ROOTCAT   ROOT.  						"new root category instead of S"
FILES.
LEXENTRIES   (DEMO ITALIAN).
TEMPLATES (DEMO ITALIAN).  				"added common ParGram templates"
RULES   (DEMO ITALIAN).
MORPHOLOGY.
GOVERNABLERELATIONS    SUBJ OBJ OBJ2 OBL OBL-?+.
SEMANTICFUNCTIONS    ADJUNCT  TOPIC FOCUS POSS STANDARD.
NONDISTRIBUTIVES    NUM PERS COORD-FORM.
EPSILON   e.
OPTIMALITYORDER NOGOOD  +PPattach PPadj +presperfect.

----

DEMO   ITALIAN   RULES (1.0)

ROOT --> 	{S
			|Simp
			|Sdrop}.

Simp --> 	e: 	(^ SUBJ PRED) = 'pro'		"syntactic rule for imperatives"
				(^ SUBJ PERS) = 2; 
			VP: (^ TNS-ASP TENSE) = pres;	"require verbs in Simp to be present tense"
			EXCL.  							"obligatory exclamation mark"
			
Sdrop -->	e								"syntactic rule for drop sentences"
			VP: ^=!;
			(PERIOD).

S 	--> 	NP:	(^ SUBJ)=!
				(! CASE)=nom;
			VP: ^=!;
			(PERIOD).

VP 	--> 	(AUX) 							"auxiliary introduced for passive and present perfect time form" 
			V: ^=!;   						"^=! usually represents the head of a phrase -> can result in errors (conflicting heads)"
			ADVP*: ! $ (^ ADJUNCT);			"Adverbs act as adjuncts and follow the verb in Italian"
			(NP: (^ OBJ)=! 					"direct object"
				 (! CASE)=acc)
			(PP: (^ OBJ-2) = !				"secondary object in ditransitives has to be a PP in Italian"
				 (! PFORM) =c a) 			"has to have preposition a introducing it"
			(PP: (^ OBL-AG) = ! 			"oblique agent of passive"
				 (! PFORM) =c da )			"PFORM must be of type da (=by)"
			PP*: { ! $ (^ ADJUNCT) 			"PPs can be all types of adjuncts"
					   (! PTYPE) =c sem 	"only semantic PPs here"
					   @(OT-MARK PPadj) 	"disprefer adjunct PPs" 
				 | (^ OBL) = ! 				"or a a general oblique"
				 }.

NP	--> 	{
			 {(D)							"optional determiner"
			 | D							"Italian requires a D PRON combination for possessive pronouns, e.g. la mia -> the my"
			   PRON: (^ PRON)= !
					 (! CASE) =c gen
					 (! PRON-TYPE) =c poss
			 }
			{AP*: ! $ (^ ADJUNCT)			"adjectives get added as adjuncts"
					  (^ GEN) = (! GEN)		"but we up their number and gender information, so they agree with the noun"
					  (^ NUM) = (! NUM)
					  (! APOS) =c pre;		"some adjectives can only appear in prenominal position"
			N
			| N 							"head of NP"
			  AP*: ! $ (^ ADJUNCT)
					   (^ GEN) = (! GEN)	
					   (^ NUM) = (! NUM)
					   (! APOS) =c post;}		"some adjectives can only appear in postnominal position"	
			PP*: ! $ (^ ADJUNCT) 			"account for potential PPs within the NP" 
					 (! PTYPE) =c sem 		"these have to be semantic though" 
					 @(OT-MARK PPattach)	"prefer PPs attached to NPs"
			| PRON}. 						"either a full NP or a pronoun"


AP	--> 	ADV*: ! $ (^ ADJUNCT);			"adjectival phrase that can appear in NPs"
			A								"adjective head that can be modified by adverbs"
			(COMMA).						"optional comma for a series of adjectives"

ADVP -->	ADV								"adverb phrase to allow for recursive adverbial constructions in VPs"
			(COMMA).

PP 	--> 	{ P: ^ = ! 						"head of PP"
				 (! PTYPE) =c sem; 			"semantic type, Adjunct"
			NP: (^ OBJ) = !
				(! CASE) = acc
			| P: ^ = ! 						"head of PP" 
				 (! PTYPE) =c nosem; 		"non-semantic type, OBL"
			NP
			}. 

----
DEMO   ITALIAN   TEMPLATES (1.0)

OT-MARK(_mark) = _mark $ o::*. 					"template to create OT marks"

PASS(FRAME) = 	{ FRAME 						"no passive, just give back subcat frame"
				 (^ PASSIVE) = - 				"no passive"
				| FRAME 						"passive"
				 (^ PASSIVE) = + 				"mark as such"
				 (^ PARTICIPLE) =c past 		"make sure to have a past participle"
				 (^ OBJ)-->(^ SUBJ) 			"obj goes to subj"
				{ (^ SUBJ)-->(^ OBL-AG)			"either make subj obl-ag"
				| (^ SUBJ)--> NULL 				"or delete"
				}
				}. 

TRANS(P) 	= 	@(PASS (^ PRED)='P<(^ SUBJ) (^ OBJ)>').		"template for transitive verbs"

INTRANS(P) 	= 	(^ PRED) = 'P<(^SUBJ)>'.					"template for intransitive verbs"	

INTRANS-OBL(P) 	= 	(^ PRED) = 'P<(^SUBJ)(^OBL)>'.			"template for intransitive verbs that can take an oblique"
		 
OPT-TRANS(P) 	= 	{@(TRANS P)|@(INTRANS P)}.				"verbs that can be both transitive and intransitive"

DITRANS(P) 	= 	@(PASS(^ PRED)='P<(^ SUBJ) (^ OBJ) (^ OBJ-2)>').		"template for ditransitive verbs"

"simple templates"
  
TENSE(T)	= 	(^ TENSE) = T.

CASE(C)		= 	(^ CASE) = C.   

MOOD(M) 	= 	(^ MOOD) = M.

"templates for different tenses"

VPRES 	= 	@(TENSE pres) 
			@(MOOD indicative).

VIMP 	= 	@(TENSE imperfect) 
			@(MOOD indicative).
			
VPRESPER	= @(TENSE presperf)
			  @(MOOD indicative).

PRED(P) = 	(^ PRED) = 'P'. 

PASTP (G)	= 	(^ PARTICIPLE) = past
				(^ SUBJ GEN) = G. 

"since we have more different verb forms in Italian, we create a new template to specify person and number"

VERB-AGR(P N) 	= 	(^ SUBJ PERS) = P
					(^ SUBJ NUM) = N.
			
"templates for nouns and pronouns"

"gender information is very important in Italian and is therefore added in the templates"
			
COMMON-NOUN-PGNS(P G N SEMA) = 	(^ PRED) = 'P'
								(^ GEN) = G
								(^ NUM) = N
								(^ PERS) = 3
								(^ NTYPE SYN) = common
								(^ NTYPE SEM) = SEMA.		"P = predicate, G = Gender, N = Number, SEMA = semantic noun type"

PROPER-NOUN-PGNS(P G N SEMA) = 	(^ PRED) = 'P'
								(^ GEN) = G
								(^ NUM) = N
								(^ PERS) = 3
								(^ NTYPE SYN) = proper
								(^ NTYPE SEM) = SEMA.

PRONOUN-PGNT(P G N PER T C) = 	(^ PRED) = 'P'
								(^ GEN) = G
								(^ NUM) = N
								(^ PERS) = PER
								(^ NTYPE SYN) = pronoun
								(^ PRON-TYPE) = T				
								(^ CASE) = C.				"P = predicate, G = Gender, N = Number, PER = person, T = pronoun type, C = case"
						

"Adverbs are invariable , meaning they do not have gender"
 						
ADVERB(P D) 	= 	(^PRED) = 'P'
					(^DEGREE) = D
					(^ADJ-TYPE) = adverb.
				
"some adjectives agree with their corresponding noun, so we have to add this information in the template."

ADJECTIVE-AGR (P G N D T) =	(^ PRED) = 'P'
							(^ GEN) = G
							(^ NUM) = N
							(^ DEGREE) = D
							(^ APOS) = T
							(^ ADJ-TYPE) = adjective.
					
"some adjectives are invariable for gender"	
				
ADJECTIVE-NO-AGR (P N D T) = (^ PRED) = 'P'
							 (^ NUM) = N
							 (^ DEGREE) = D
							 (^ APOS) = T
							 (^ ADJ-TYPE) = adjective.
 
PREP(P) = 	{ (^ PRED) = 'P<(^OBJ)>' 
			  (^ PTYPE) = sem
			| (^ PTYPE) = nosem
			  (^ PFORM) = P
			}.


DEF =  (^ DEF). 


----

DEMO ITALIAN LEXICON (1.0)

"nouns"

Franca    	N * @(PROPER-NOUN-PGNS Franca fem sg name).

Giacomo		N * @(PROPER-NOUN-PGNS Giacomo masc sg name).

mela		N * @(COMMON-NOUN-PGNS mela fem sg count).

mele		N * @(COMMON-NOUN-PGNS mela fem pl count).

bagno		N * @(COMMON-NOUN-PGNS bagno masc sg count).

scuola		N * @(COMMON-NOUN-PGNS scuola fem sg count).

libro		N * @(COMMON-NOUN-PGNS libro masc sg count).

libri		N * @(COMMON-NOUN-PGNS libro masc pl count).

gallo		N * @(COMMON-NOUN-PGNS gallo masc sg count).

premio		N * @(COMMON-NOUN-PGNS premio masc sg count).

finestra	N * @(COMMON-NOUN-PGNS finestra fem sg count).

"all verbs here"

mangio		V * {@(OPT-TRANS mangiare)
				@VPRES
				@(VERB-AGR 1 sg)
				|
				@(OPT-TRANS mangiare)
				@VPRES
				@(VERB-AGR 1 sg)
				(^ SUBJ PRED) = 'pro'}.

mangia		V * {@(OPT-TRANS mangiare)
				@VPRES
				@(VERB-AGR 3 sg)
				|
				@(OPT-TRANS mangiare)
				@VPRES
				@(VERB-AGR 3 sg)
				(^ SUBJ PRED) = 'pro'}.

mangiata	V * @(TRANS mangiare)
				{@(PASTP fem)
				@(VERB-AGR 3 sg)
				|@VPRESPER
				@(OT-MARK presperfect)}.
				
legge		V * {@(OPT-TRANS leggere)
				@VPRES
				@(VERB-AGR 3 sg)
				|
				@(OPT-TRANS leggere)
				@VPRES
				@(VERB-AGR 3 sg)
				(^ SUBJ PRED) = 'pro'}.
				
riceve		V * {@(TRANS ricevere)
				@VPRES
				@(VERB-AGR 3 sg)
				|
				@(TRANS ricevere)
				@VPRES
				@(VERB-AGR 3 sg)
				(^ SUBJ PRED) = 'pro'}.
				
apri		V * {@(TRANS aprire)
				@VPRES
				@(VERB-AGR 2 sg)
				|
				@(TRANS aprire)
				@VPRES
				@(VERB-AGR 2 sg)
				(^ SUBJ PRED) = 'pro'}.
				
apre		V * {@(TRANS aprire)
				@VPRES
				@(VERB-AGR 3 sg)
				|
				@(TRANS aprire)
				@VPRES
				@(VERB-AGR 3 sg)
				(^ SUBJ PRED) = 'pro'}.
				
scritto		V * @(TRANS scrivere)
				{@(PASTP masc)
				@(VERB-AGR 3 sg)
				|@VPRESPER
				@(OT-MARK presperfect)}.			"OT-Mark to prefer present perfect reading over passive reading when applicable"
				
scriva		V * {@(TRANS scrivere)
				@VIMP
				@(VERB-AGR 3 sg)
				|
				@(TRANS scrivere)
				@VIMP
				@(VERB-AGR 3 sg)
				(^ SUBJ PRED) = 'pro'}.

dato		V * @(DITRANS dare)
				{@(PASTP masc)
				@(VERB-AGR 3 sg)
				|@VPRESPER
				@(OT-MARK presperfect)}.

danno		V * {@(DITRANS dare)
				@VPRES
				@(VERB-AGR 3 pl)
				|
				@(DITRANS dare)
				@VPRES
				@(VERB-AGR 3 pl)
				(^ SUBJ PRED) = 'pro'}.
				
"adjectives"

nera 			A * @(ADJECTIVE-AGR nero fem sg positive post).

nero 			A * @(ADJECTIVE-AGR nero masc sg positive post).

primo			A * @(ADJECTIVE-AGR primo masc sg positive pre).

prima			A * @(ADJECTIVE-AGR primo fem sg positive pre).

verde 			A * @(ADJECTIVE-NO-AGR verde sg positive post).

verdi 			A * @(ADJECTIVE-NO-AGR verde pl positive post).

interessante	A * @(ADJECTIVE-NO-AGR interessante sg positive post).

interessanti	A * @(ADJECTIVE-NO-AGR interessante pl positive post).

vecchio			A * {@(ADJECTIVE-AGR vecchio masc sg positive pre)
					|@(ADJECTIVE-AGR vecchio masc sg positive post)}.

"adverbs"

veramente	ADV * @(ADVERB vero positive).

proprio		ADV * @(ADVERB proprio positive).

molto		ADV * @(ADVERB molto positive).

lentamente	ADV * @(ADVERB lentamente positive).

"pronouns"

io		PRON * {@(PRONOUN-PGNT Io masc sg 1 pers nom)
				|@(PRONOUN-PGNT Io fem sg 1 pers nom)}.
				
tu 		PRON * {@(PRONOUN-PGNT tu masc sg 2 pers nom)
				|@(PRONOUN-PGNT tu fem sg 2 pers nom)}.
				
lui		PRON * @(PRONOUN-PGNT lui masc sg 3 pers nom).

lei		PRON * @(PRONOUN-PGNT lei fem sg 3 pers nom).

noi		PRON * @(PRONOUN-PGNT noi neu pl 1 pers nom).

voi		PRON * @(PRONOUN-PGNT voi neu pl 2 pers nom).

loro	PRON * @(PRONOUN-PGNT loro neu pl 3 pers nom).

mia		PRON * @(PRONOUN-PGNT Io fem sg 1 poss gen).

mio		PRON * @(PRONOUN-PGNT Io masc sg 1 poss gen).

"determiners"

la 	D * (^ DEF) = +
		(^ GEN) = fem
		(^ NUM) = sg.
		
le 	D * (^ DEF) = +
		(^ GEN) = fem
		(^ NUM) = pl.
		
il 	D * (^ DEF) = +
		(^ GEN) = masc
		(^ NUM) = sg.
		
i	D * (^ DEF) = +
		(^ GEN) = masc
		(^ NUM) = pl.

una  D *  (^ DEF) = -
		  (^ GEN) = fem
		  (^ NUM) = sg.
		  
uno  D *  (^ DEF) = -
		  (^ GEN) = masc
		  (^ NUM) = sg.

"prepositions"

in 	  P * @(PREP in).

da	  P * @(PREP da).

con   P * @(PREP con).

su	  P * @(PREP su).

a	  P * @(PREP a).

"auxiliaries"

E 	AUX * @VPRES						"the actual form é doesn't work in xle so for this project é = E"
		  @(VERB-AGR 3 sg).

ha	V   * @(TRANS avere)
		  @VPRES						"ha (has) can be both an auxiliary and a copula, here specified as V"
		  @(VERB-AGR 3 sg);
	
	AUX	* @VPRESPER
		  @(VERB-AGR 3 sg).
		  
abbiamo	V   * @VPRES
			  @(VERB-AGR 1 pl);
	
		AUX	* @VPRESPER
		      @(VERB-AGR 1 pl).

"punctuation, now with some functional information"


. PERIOD * (^ STMT-TYPE) = declarative. 

, COMMA  * . 

!  EXCL * (^ STMT-TYPE) = exclamation. 

----